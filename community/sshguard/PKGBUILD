# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=sshguard
pkgname=('sshguard-pf' 'sshguard-ipfw')
pkgver=1.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.sshguard.net/"
license=('GPL')
depends=('freebsd-world')
options=(zipman)
source=("http://downloads.sourceforge.net/sourceforge/sshguard/sshguard-$pkgver.tar.bz2"
        'sshguard.confd'
        'sshguard.initd'
        'sshguard')
sha256sums=('b537f8765455fdf8424f87d4bd695e5b675b88e5d164865452137947093e7e19'
            '4b35c9fe87f67216d246f640d106f9d018fe95c00cf806c4795a515196b945c6'
            '50d1bb337ffcce8c952770b050746c5286ce0a1eb8626a2882be177354cbd22c'
            '30d40252d40eb8b7dcdbfc457d7109a6aebeae651a52c547bf264050295bef89')

prepare() {
	cd "$srcdir/$pkgbase-$pkgver"
	gsed -i '' 's|^DAYNO.*|DAYNO       [0-9]?[1-9]|' src/parser/attack_scanner.l
}

build() {
	cd "$srcdir"
	cp -a "$pkgbase-$pkgver"{,-ipfw}

	cd "$srcdir/$pkgbase-$pkgver"
	./configure --prefix=/usr --sbindir=/usr/sbin --with-firewall=pf
	make

	cd "$srcdir/$pkgbase-$pkgver-ipfw"
	./configure --prefix=/usr --sbindir=/usr/sbin --with-firewall=ipfw
	make
}

package_sshguard-pf() {
	pkgname='sshguard-pf'
	pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with pf"
	provides=('sshguard')
	conflicts=('sshguard')
	cd "$srcdir/$pkgbase-$pkgver"
	sed -e "s|firewall_backend|pf|" sshguard.initd > sshguard.initd.pf

	make DESTDIR="$pkgdir" install
	install -dm755 ${pkgdir}/etc/{conf,init,rc}.d/
	install -m655 sshguard.confd ${pkgdir}/etc/conf.d/sshguard
	install -m655 sshguard.initd.pf ${pkgdir}/etc/init.d/sshguard
	install -m655 sshguard ${pkgdir}/etc/rc.d/sshguard
}

package_sshguard-ipfw() {
	pkgname='sshguard-ipfw'
	pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with ipfw"
	provides=('sshguard')
	conflicts=('sshguard')
	cd "$srcdir/$pkgbase-$pkgver-ipfw"
	sed -e "s|firewall_backend|ipfw|" sshguard.initd > sshguard.initd.ipfw

	make DESTDIR="$pkgdir" install
	install -dm755 ${pkgdir}/etc/{conf,init,rc}.d/
	install -m655 sshguard.confd ${pkgdir}/etc/conf.d/sshguard
	install -m655 sshguard.initd.ipfw ${pkgdir}/etc/init.d/sshguard
	install -m655 sshguard ${pkgdir}/etc/rc.d/sshguard
}

# :vim set tabstop=2
